preprocess_dir = config["elvers_directories"]["preprocess"]
sigsketch_dir = os.path.join(out_dir, config['sourmash_sketch_reads']['output_dir'])

sigsketch_params = config['sourmash_sketch_reads']['params']

def get_reads(w):
    if not is_single_end(w.sample):
        return os.path.join(preprocess_dir, "khmer", f'{w.sample}.paired.khmer.fq.gz')
    return os.path.join(preprocess_dir, "khmer", f'{w.sample}.se.khmer.fq.gz')

rule sourmash_compute_pe_interleaved:
    """
    Computing a MinHash signature of the kmer-trimmed with Sourmash
    """
    input: get_reads 
    output: os.path.join(sigsketch_dir, "{sample}.paired.fq.sig")
    message:
        """--- Computing a MinHash signature of the kmer-trimmed reads with Sourmash ---"""
    threads: 6
    params:
        k_size = sigsketch_params.get('k_size', 31),
        scaled  = sigsketch_params.get('scaled', 1000),
        extra = sigsketch_params.get('extra', '')
    log: os.path.join(logs_dir, 'sourmash', '{sample}_paired.log')
    benchmark: os.path.join(benchmarks_dir, 'sourmash', '{sample}_paired.benchmark')
    conda: 
        'environment.yml'
    script: 
        'sourmash-wrapper.py'

rule sourmash_compute_se:
    """
    Computing a MinHash signature of the kmer-trimmed reads with Sourmash
    """
    input: get_reads 
    output: os.path.join(sigsketch_dir, "{sample}.se.fq.sig")
    message:
        """--- Computing a MinHash signature of the kmer-trimmed reads with Sourmash ---"""
    threads: 6
    params:
        k_size = sigsketch_params.get('k_size', 31),
        scaled  = sigsketch_params.get('scaled', 1000),
        extra = sigsketch_params.get('extra', '')
    log: os.path.join(logs_dir, 'sourmash', '{sample}.log')
    benchmark: os.path.join(benchmarks_dir, 'sourmash', '{sample}_se.benchmark')
    conda: 
        'environment.yml'
    script: 
        'sourmash-wrapper.py'
